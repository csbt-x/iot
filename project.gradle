// Common configuration applied to all projects

import java.nio.file.Paths

import static org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS
import static org.apache.tools.ant.taskdefs.condition.Os.isFamily
import static org.jetbrains.gradle.ext.ShortenCommandLine.MANIFEST
import org.jetbrains.gradle.ext.Application
import org.jetbrains.gradle.ext.JUnit

// Build plugins
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.1"
    }
}

// Configure IDEA project (needs to be applied to the root project only)
if (project == rootProject) {
    try {
        apply plugin: "org.jetbrains.gradle.plugin.idea-ext"
    } catch(Exception e) {

    }

    if (project.hasProperty("idea") && idea.project) {
        // IDEA settings
        idea.project.settings {
            compiler {
                javac {
                    javacAdditionalOptions "-parameters"
                }
            }
            runConfigurations {
                defaults(JUnit) {
                    shortenCommandLine = MANIFEST
                    workingDirectory = (isCustomProject() ? project(":openremote").projectDir.toString() : projectDir.toString())
                }
                defaults(Application) {
                    mainClass = 'org.openremote.manager.Main'
                    shortenCommandLine = MANIFEST
                    workingDirectory = (isCustomProject() ? project(":openremote").projectDir.toString() : projectDir.toString())
                }
                "Empty"(Application) {
                    moduleName = getProject().idea.module.name + (isCustomProject() ? ".openremote.manager.main" : ".manager.main")
                }
                "Test Setup"(Application) {
                    moduleName = getProject().idea.module.name + (isCustomProject() ? ".openremote.test.main" : ".test.main")
                }
                "Empty with Proxy"(Application) {
                    moduleName = getProject().idea.module.name + (isCustomProject() ? ".openremote.manager.main" : ".manager.main")
                    envs = [
                            WEBSERVER_LISTEN_HOST: "0.0.0.0",
                            EXTERNAL_URL: "https://localhost:8443"
                    ]
                }
                "Test Setup with Proxy"(Application) {
                    moduleName = getProject().idea.module.name + (isCustomProject() ? ".openremote.test.main" : ".test.main")
                    envs = [
                            WEBSERVER_LISTEN_HOST: "0.0.0.0",
                            EXTERNAL_URL: "https://localhost:8443"
                    ]
                }
            }
        }

        if (isCustomProject()) {
            idea.project.settings.runConfigurations {
                "Custom Deployment"(Application) {
                    moduleName = "${getProject().idea.module.name}.setup.main"
                    envs = [
                        MAP_SETTINGS_PATH: "../deployment/map/mapsettings.json",
                        MAP_TILES_PATH: "../deployment/map/mapdata.mbtiles",
                        CUSTOM_APP_DOCROOT: "../deployment/manager/app"
                    ]
                }
            }
        }
    }
}

// Give test projects more memory (Gradle 5 reduced this to 512MB)
subprojects {
    tasks.withType(Test) {
        maxHeapSize = "2g"
    }
}

// Default repositories for dependency resolution
repositories {
    maven {
        url = "https://repo.osgeo.org/repository/release/"
    }
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url "https://pkgs.dev.azure.com/OpenRemote/OpenRemote/_packaging/OpenRemote/maven/v1"
    }
    maven {
        url "https://jitpack.io"
    }
}

// Eclipse needs help
apply plugin: "eclipse"

// Intellij needs help
apply plugin: 'idea'
// Use the same output directories in IDE as in gradle
idea {
    module {
        outputDir file('build/classes/main')
        testOutputDir file('build/classes/test')
        excludeDirs += file(".node")
        excludeDirs += file("node_modules")
        excludeDirs += file("dist")
        excludeDirs += file("lib")
        excludeDirs += file("build")
    }
}



// Helper functions for project/task resolution when the main 
// repo is checked out as a git submodule and therefore a subproject
def isCustomProject() {
    findProject(":openremote") != null
}

def resolvePath(String path) {
    isCustomProject() ? ":openremote" + path : path
}

def resolveProject(String path) {
    project(resolvePath(path))
}

def resolveTask(String path) {
    tasks.getByPath(resolvePath(path))
}

def getYarnInstallTask() {
    if (isCustomProject()) {
        def customPackageJsonFile = Paths.get(rootProject.projectDir.path, "package.json").toFile()
        if (!customPackageJsonFile.exists()) {
            // No custom project yarn package.json so use standard openremote repo package.json
            return resolveTask(":yarnInstall")
        } else {
            return tasks.getByPath(":yarnInstall")
        }
    } else {
        // Just use openremote repo yarn install
        resolveTask(":yarnInstall")
    }
}

ext {
    resolvePath = this.&resolvePath
    resolveProject = this.&resolveProject
    resolveTask = this.&resolveTask
    isCustomProject = this.&isCustomProject
}

if (project.convention.findPlugin(JavaPluginConvention)) {
    // Change the output directory for the main and test source sets back to the old path
    sourceSets.main.java.outputDir = new File(buildDir, "classes/main")
    sourceSets.test.java.outputDir = new File(buildDir, "classes/test")
}

// Configure versions in gradle.properties (putting a gradle.properties file
// in a subproject only overrides root properties of same name for the actual
// subproject, not for its children!)
version = hasProperty("openremoteVersion") ? openremoteVersion : projectVersion

// Add UI tasks
ext.npmCommand = {
    cmd ->
        isFamily(FAMILY_WINDOWS) ? "${cmd}.cmd" : cmd
}

// Add yarn tasks
task yarnInstall(type: Exec){
    commandLine npmCommand("yarn"), "install"
}
task yarnInstallForce(type: Exec){
    commandLine npmCommand("yarn"), "install", "--force"
}

// Add npm tasks
task npmInstall(type: Exec){
    commandLine npmCommand("npm"), "install"
}
task npmClean(type: Exec){
    dependsOn getYarnInstallTask()
    commandLine npmCommand("npm"), "run", "clean"
}
task npmBuild(type: Exec){
    mustRunAfter npmClean
    dependsOn getYarnInstallTask()
    commandLine npmCommand("npm"), "run", "build"
}
task npmTest(type: Exec){
    dependsOn getYarnInstallTask()
    commandLine npmCommand("npm"), "run", "test"
}
task npmServe(type: Exec){
    dependsOn getYarnInstallTask()
    commandLine npmCommand("npm"), "run", "serve"
}
task npmPrepare(type: Exec){
    dependsOn getYarnInstallTask()
    commandLine npmCommand("npm"), "run", "prepublishOnly"
}
task npmPublish(type: Exec){
    dependsOn getYarnInstallTask()
    commandLine npmCommand("npm"), "publish"
}
task npmServeProduction(type: Exec) {
    dependsOn getYarnInstallTask()
    commandLine npmCommand("npm"), "run", "serveProduction"
}

// Add typescript tasks
task tscWatch(type: Exec) {
    commandLine npmCommand("npx"), "tsc", "-b", "--watch"
}

// A task for copying a custom java projects libs that aren't part of the manager dependencies
task copyLibs {
    doLast {

        if (project.configurations.find { it.name == "runtimeClasspath" } != null) {
            // Get all dependencies that are already part of the openremote manager and exclude these from the libs dir
            // otherwise they will appear on the classpath twice (once in manager app dir and then again in extensions)
            def excludeDependencies = resolveProject(":manager").configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts
            def customLibs = project.configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts.findAll { dep -> excludeDependencies.find { it == dep } == null && dep.name != "openremote-manager" }.collect { it.file }
            copy {
                from customLibs
                into "${project.buildDir}/image/manager/extensions"
            }
        }
    }
}

// Configure Java build
plugins.withType(JavaPlugin).whenPluginAdded {

    // Use Java 17
    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        def warnLogFile = file("$buildDir/${name}Warnings.log")
        logging.addStandardErrorListener(new StandardOutputListener() {
            void onOutput(CharSequence output) {
                warnLogFile << output
            }
        })
        options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation"]
        options.encoding = 'UTF-8'
    }

    // JAR/ZIP base name is the fully qualified subproject name
    archivesBaseName = "${rootProject.name}${path.replaceAll(":", "-")}"
}
